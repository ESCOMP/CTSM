module test_dominant_soil_color

  ! Tests of mksoilUtilsMod: dominant_soil_color

  use pfunit_mod
  use mksoilUtilsMod
  use shr_kind_mod , only : r8 => shr_kind_r8
  use mkgridmapMod, only : gridmap_type, gridmap_clean, for_test_create_gridmap

  implicit none

  @TestCase
  type, extends(TestCase) :: tdsc
     type(gridmap_type) :: gridmap
   contains
     procedure :: setUp
     procedure :: tearDown
     procedure :: createGridmap1dst
  end type tdsc

  real(r8), parameter :: tol = 1.e-13_r8

contains

  subroutine setUp(this)
    class(tdsc), intent(inout) :: this
  end subroutine setUp

  subroutine tearDown(this)
    class(tdsc), intent(inout) :: this
    call gridmap_clean(this%gridmap)
  end subroutine tearDown

  subroutine createGridmap1dst(this, wovr)
    ! Create this%gridmap with a single destination point
    class(tdsc), intent(inout) :: this
    real(r8), intent(in) :: wovr(:) ! overlap weights

    integer :: i
    integer :: npts
    integer :: src_indx(size(wovr))
    integer :: dst_indx(size(wovr))

    dst_indx(:) = 1
    npts = size(wovr)
    src_indx(:) = [(i, i = 1, npts)]

    call for_test_create_gridmap(this%gridmap, na = npts, nb = 1, ns = npts, &
         src_indx = src_indx, dst_indx = dst_indx, wovr = wovr)
  end subroutine createGridmap1dst

  @Test
  subroutine equalWeights(this)
    ! Four inputs with equal weight; two of one class, one of each of two other classes
    class(tdsc), intent(inout) :: this
    integer :: mask_i(4)
    integer :: soil_color_i(4)
    integer :: soil_color_o(1)

    call this%createGridmap1dst([0.25_r8, 0.25_r8, 0.25_r8, 0.25_r8])
    mask_i(:) = 1
    soil_color_i(:) = [1, 2, 2, 3]

    call dominant_soil_color(this%gridmap, mask_i, soil_color_i, 20, soil_color_o)

    @assertEqual(2, soil_color_o(1))
  end subroutine equalWeights

  @Test
  subroutine inequalWeights(this)
    ! Four inputs with inequal weight
    class(tdsc), intent(inout) :: this
    integer :: mask_i(4)
    integer :: soil_color_i(4)
    integer :: soil_color_o(1)

    call this%createGridmap1dst([0.5_r8, 0.2_r8, 0.2_r8, 0.1_r8])
    mask_i(:) = 1
    soil_color_i(:) = [3, 1, 1, 2]

    call dominant_soil_color(this%gridmap, mask_i, soil_color_i, 20, soil_color_o)

    @assertEqual(3, soil_color_o(1))
  end subroutine inequalWeights

  @Test
  subroutine noColor(this)
    ! No color in input
    class(tdsc), intent(inout) :: this
    integer :: mask_i(4)
    integer :: soil_color_i(4)
    integer :: soil_color_o(1)

    call this%createGridmap1dst([0.25_r8, 0.25_r8, 0.25_r8, 0.25_r8])
    ! Some points are inside the mask with color = 0, other points are outside the mask
    mask_i(:) = [1, 0, 0, 1]
    soil_color_i(:) = [0, 1, 1, 0]

    call dominant_soil_color(this%gridmap, mask_i, soil_color_i, 20, soil_color_o)

    @assertEqual(15, soil_color_o(1))
  end subroutine noColor

  @Test
  subroutine noColorInFirstPoints(this)
    ! No color in the first points, but a color in the last point
    class(tdsc), intent(inout) :: this
    integer :: mask_i(4)
    integer :: soil_color_i(4)
    integer :: soil_color_o(1)

    call this%createGridmap1dst([0.25_r8, 0.25_r8, 0.25_r8, 0.25_r8])
    ! Some points are inside the mask with color = 0, other points are outside the mask
    mask_i(:) = 1
    soil_color_i(:) = [0, 0, 0, 1]

    call dominant_soil_color(this%gridmap, mask_i, soil_color_i, 20, soil_color_o)

    @assertEqual(1, soil_color_o(1))
  end subroutine noColorInFirstPoints

  @Test
  subroutine noColorInLastPoints(this)
    ! No color in the last points, but a color in the first point
    class(tdsc), intent(inout) :: this
    integer :: mask_i(4)
    integer :: soil_color_i(4)
    integer :: soil_color_o(1)

    call this%createGridmap1dst([0.25_r8, 0.25_r8, 0.25_r8, 0.25_r8])
    ! Some points are inside the mask with color = 0, other points are outside the mask
    mask_i(:) = 1
    soil_color_i(:) = [1, 0, 0, 0]

    call dominant_soil_color(this%gridmap, mask_i, soil_color_i, 20, soil_color_o)

    @assertEqual(1, soil_color_o(1))
  end subroutine noColorInLastPoints

end module test_dominant_soil_color
