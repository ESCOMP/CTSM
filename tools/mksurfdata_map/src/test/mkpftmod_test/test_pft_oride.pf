module test_pft_oride
  
  ! Tests of mkpftMod: pft_override functions

  use pfunit_mod

  use shr_kind_mod, only : r8 => shr_kind_r8
  use mkpftMod
  use mkvarctl, only : numpft
  use mkvarpar, only : numstdpft
  use mkpftConstantsMod, only : c3cropindex, c3irrcropindex

  implicit none
  save

  @TestCase
  type, extends(TestCase) :: TestPFTORide
     type(pft_oride) :: pftoverride
   contains
     procedure :: setUp
     procedure :: tearDown
  end type TestPFTORide

  logical :: zero_out, all_veg_l

contains

  subroutine setUp(this)
    class(TestPFTORide), intent(inout) :: this

    pft_idx(:) = -1
    pft_frc(:) = 0.0_r8
    zero_out = .false.
    numpft = numstdpft
    all_veg_l = .false.
    call mkpftInit( zero_out, all_veg_l )
    this%pftoverride = pft_oride( )
  end subroutine setUp

  subroutine tearDown(this)
    class(TestPFTORide), intent(inout) :: this

    call this%pftoverride%Clean()

  end subroutine tearDown

  @Test
  subroutine test_runmkpftZero(this)
    class(TestPFTORide), intent(inout) :: this

    zero_out = .true.
    all_veg_l = .false.
    call mkpftInit( zero_out, all_veg_l )
    call this%pftoverride%InitZeroOut()
    @assertEqual(   0.0_r8, this%pftoverride%crop   )
    @assertEqual(   0.0_r8, this%pftoverride%natveg )
    @assertEqual( 100.0_r8, sum(this%pftoverride%cft))
    @assertEqual( 100.0_r8, sum(this%pftoverride%natpft))

  end subroutine test_runmkpftZero

  @Test
  subroutine test_runSetpft(this)
    class(TestPFTORide), intent(inout) :: this
    integer, parameter :: pftidx = 1

    pft_idx(0) = pftidx
    pft_frc(0) = 100.0_r8
    zero_out = .false.
    all_veg_l  = .true.
    call mkpftInit( zero_out, all_veg_l )
    call this%pftoverride%InitAllPFTIndex()
    @assertEqual( 0.0_r8,   this%pftoverride%crop )
    @assertEqual( 100.0_r8, this%pftoverride%natveg )
    @assertEqual( 100.0_r8, sum(this%pftoverride%cft) )
    @assertEqual( 100.0_r8, sum(this%pftoverride%natpft) )
    @assertEqual( 100.0_r8, this%pftoverride%natpft(pftidx) )

  end subroutine test_runSetpft

  @Test
  subroutine test_runSetCrop(this)
    class(TestPFTORide), intent(inout) :: this
    integer :: cftidx

    cftidx = c3cropindex
    pft_idx(0) = cftidx
    pft_frc(0) = 100.0_r8
    zero_out = .false.
    all_veg_l  = .true.
    call mkpftInit( zero_out, all_veg_l )
    call this%pftoverride%InitAllPFTIndex()
    @assertEqual( 100.0_r8,   this%pftoverride%crop )
    @assertEqual(   0.0_r8, this%pftoverride%natveg )
    @assertEqual( 100.0_r8, sum(this%pftoverride%cft) )
    @assertEqual( 100.0_r8, sum(this%pftoverride%natpft) )
    @assertEqual( 100.0_r8, this%pftoverride%cft(numpft-cftidx) )

  end subroutine test_runSetCrop

  @Test
  subroutine test_runSetMix(this)
    class(TestPFTORide), intent(inout) :: this
    integer :: cftidx, cftidx2, pftidx2
    integer, parameter :: pftidx = 1

    zero_out = .false.
    pftidx2 = c3cropindex-1
    cftidx = c3cropindex
    cftidx2 = c3irrcropindex
    pft_idx(0:3) = (/ pftidx,  pftidx2, cftidx,  cftidx2 /)
    pft_frc(0:3) = (/ 25.0_r8, 25.0_r8, 25.0_r8, 25.0_r8  /)
    all_veg_l  = .true.
    call mkpftInit( zero_out, all_veg_l )
    call this%pftoverride%InitAllPFTIndex()
    @assertEqual(  50.0_r8,   this%pftoverride%crop )
    @assertEqual(  50.0_r8, this%pftoverride%natveg )
    @assertEqual( 100.0_r8, sum(this%pftoverride%cft) )
    @assertEqual( 100.0_r8, sum(this%pftoverride%natpft) )
    @assertEqual(  50.0_r8, this%pftoverride%natpft(pftidx) )
    @assertEqual(  50.0_r8, this%pftoverride%natpft(pftidx2) )
    @assertEqual(  50.0_r8, this%pftoverride%cft(1) )
    @assertEqual(  50.0_r8, this%pftoverride%cft(2) )

  end subroutine test_runSetMix

end module test_pft_oride
