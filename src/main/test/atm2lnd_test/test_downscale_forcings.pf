module test_downscale_forcings

  ! Tests of atm2lndMod: downscale_forcings

  use pfunit_mod
  use atm2lndMod
  use shr_kind_mod , only : r8 => shr_kind_r8
  use unittestSubgridMod
  use unittestSimpleSubgridSetupsMod
  use unittestArrayMod
  use atm2lndType, only : atm2lnd_type, atm2lnd_params_type
  use TopoMod, only : topo_type
  use decompMod, only : bounds_type
  use filterColMod

  implicit none

  type, extends(topo_type) :: topo_type_always_downscale
   contains
     procedure, public :: DownscaleFilterc
  end type topo_type_always_downscale

  @TestCase
  type, extends(TestCase) :: TestDownscaleForcings
     type(atm2lnd_type) :: atm2lnd_inst
     type(topo_type_always_downscale) :: topo_inst
   contains
     procedure :: setUp
     procedure :: tearDown
     procedure :: create_atm2lnd
     procedure :: create_topo
  end type TestDownscaleForcings

  real(r8), parameter :: tol = 1.e-13_r8

contains

  ! ========================================================================
  ! Methods on topo_type_always_downscale
  ! ========================================================================

  function DownscaleFilterc(this, bounds) result(filter)
    ! Override topo_type's DownscaleFilterc
    type(filter_col_type) :: filter  ! function result
    class(topo_type_always_downscale), intent(in) :: this
    type(bounds_type), intent(in) :: bounds

    filter = col_filter_from_logical_array(bounds, &
         col_array(.true.))
  end function DownscaleFilterc

  ! ========================================================================
  ! Test helper methods
  ! ========================================================================

  subroutine setUp(this)
    class(TestDownscaleForcings), intent(inout) :: this
  end subroutine setUp

  subroutine tearDown(this)
    class(TestDownscaleForcings), intent(inout) :: this

    call this%atm2lnd_inst%Clean()
    call this%topo_inst%Clean()
    call unittest_subgrid_teardown()
  end subroutine tearDown

  subroutine create_atm2lnd(this, forc_topo, forc_t, forc_th, forc_q, forc_pbot, forc_rho, &
       forc_lwrad, forc_rain, forc_snow, glcmec_downscale_longwave)
    ! Initializes this%atm2lnd_inst and sets gridcell-level, non-downscaled forcing
    ! fields based on inputs. Excluded inputs are given a default value
    class(TestDownscaleForcings), intent(inout) :: this
    real(r8), intent(in) :: forc_topo(:)
    real(r8), intent(in), optional :: forc_t(:)
    real(r8), intent(in), optional :: forc_th(:)
    real(r8), intent(in), optional :: forc_q(:)
    real(r8), intent(in), optional :: forc_pbot(:)
    real(r8), intent(in), optional :: forc_rho(:)
    real(r8), intent(in), optional :: forc_lwrad(:)
    real(r8), intent(in), optional :: forc_rain(:)
    real(r8), intent(in), optional :: forc_snow(:)

    ! If not provided, assumed to be true
    logical, intent(in), optional :: glcmec_downscale_longwave

    real(r8), parameter :: forc_t_default = 301._r8
    real(r8), parameter :: forc_th_default = 302._r8
    real(r8), parameter :: forc_q_default = 0.01_r8
    real(r8), parameter :: forc_pbot_default = 100000._r8
    real(r8), parameter :: forc_rho_default = 1.1_r8
    real(r8), parameter :: forc_lwrad_default = 101._r8
    real(r8), parameter :: forc_rain_default = 21._r8
    real(r8), parameter :: forc_snow_default = 22._r8

    real(r8), parameter :: forc_hgt = 2._r8

    logical :: l_glcmec_downscale_longwave
    type(atm2lnd_params_type) :: atm2lnd_params

    ! ------------------------------------------------------------------------

    if (present(glcmec_downscale_longwave)) then
       l_glcmec_downscale_longwave = glcmec_downscale_longwave
    else
       l_glcmec_downscale_longwave = .true.
    end if

    atm2lnd_params = atm2lnd_params_type( &
         repartition_rain_snow = .false., &
         glcmec_downscale_longwave = l_glcmec_downscale_longwave)
    call this%atm2lnd_inst%InitForTesting(bounds, atm2lnd_params)
    this%atm2lnd_inst%forc_topo_grc(bounds%begg:bounds%endg) = forc_topo(:)

    if (present(forc_t)) then
       this%atm2lnd_inst%forc_t_not_downscaled_grc(bounds%begg:bounds%endg) = forc_t(:)
    else
       this%atm2lnd_inst%forc_t_not_downscaled_grc(bounds%begg:bounds%endg) = forc_t_default
    end if

    if (present(forc_th)) then
       this%atm2lnd_inst%forc_th_not_downscaled_grc(bounds%begg:bounds%endg) = forc_th(:)
    else
       this%atm2lnd_inst%forc_th_not_downscaled_grc(bounds%begg:bounds%endg) = forc_th_default
    end if

    if (present(forc_q)) then
       this%atm2lnd_inst%forc_q_not_downscaled_grc(bounds%begg:bounds%endg) = forc_q(:)
    else
       this%atm2lnd_inst%forc_q_not_downscaled_grc(bounds%begg:bounds%endg) = forc_q_default
    end if

    if (present(forc_pbot)) then
       this%atm2lnd_inst%forc_pbot_not_downscaled_grc(bounds%begg:bounds%endg) = forc_pbot(:)
    else
       this%atm2lnd_inst%forc_pbot_not_downscaled_grc(bounds%begg:bounds%endg) = forc_pbot_default
    end if

    if (present(forc_rho)) then
       this%atm2lnd_inst%forc_rho_not_downscaled_grc(bounds%begg:bounds%endg) = forc_rho(:)
    else
       this%atm2lnd_inst%forc_rho_not_downscaled_grc(bounds%begg:bounds%endg) = forc_rho_default
    end if

    if (present(forc_lwrad)) then
       this%atm2lnd_inst%forc_lwrad_not_downscaled_grc(bounds%begg:bounds%endg) = forc_lwrad(:)
    else
       this%atm2lnd_inst%forc_lwrad_not_downscaled_grc(bounds%begg:bounds%endg) = forc_lwrad_default
    end if

    if (present(forc_rain)) then
       this%atm2lnd_inst%forc_rain_not_downscaled_grc(bounds%begg:bounds%endg) = forc_rain(:)
    else
       this%atm2lnd_inst%forc_rain_not_downscaled_grc(bounds%begg:bounds%endg) = forc_rain_default
    end if

    if (present(forc_snow)) then
       this%atm2lnd_inst%forc_snow_not_downscaled_grc(bounds%begg:bounds%endg) = forc_snow(:)
    else
       this%atm2lnd_inst%forc_snow_not_downscaled_grc(bounds%begg:bounds%endg) = forc_snow_default
    end if

    this%atm2lnd_inst%forc_hgt_grc(bounds%begg:bounds%endg) = forc_hgt
  end subroutine create_atm2lnd

  subroutine create_topo(this, topo_col)
    ! Initializes this%topo_inst, and sets topo_col
    class(TestDownscaleForcings), intent(inout) :: this
    real(r8), intent(in) :: topo_col(:)

    call this%topo_inst%Init(bounds)
    this%topo_inst%topo_col(bounds%begc:bounds%endc) = topo_col(:)
  end subroutine create_topo

  ! ========================================================================
  ! Begin actual tests
  ! ========================================================================

  ! ------------------------------------------------------------------------
  ! Basic sanity tests
  ! ------------------------------------------------------------------------

  @Test
  subroutine topo_greaterThan_atmTopo_gives_colder_forc_t(this)
    class(TestDownscaleForcings), intent(inout) :: this
    real(r8), allocatable :: eflx_sh_precip_conversion(:)
    real(r8), parameter :: atm_topo = 17._r8
    real(r8), parameter :: forc_t = 301._r8

    ! Setup
    call setup_single_veg_patch(pft_type=1)
    call this%create_atm2lnd( &
         forc_topo = [atm_topo], &
         forc_t = [forc_t])
    call this%create_topo(topo_col = [atm_topo + 10._r8])
    eflx_sh_precip_conversion = col_array()

    ! Exercise
    call downscale_forcings(bounds, this%topo_inst, this%atm2lnd_inst, &
         eflx_sh_precip_conversion)

    ! Verify
    @assertGreaterThan(forc_t, this%atm2lnd_inst%forc_t_downscaled_col(begc))
  end subroutine topo_greaterThan_atmTopo_gives_colder_forc_t

  ! ------------------------------------------------------------------------
  ! Tests involving longwave normalization
  ! ------------------------------------------------------------------------

  @Test
  subroutine twoTopos_at_atmTopo_give_nearlyIdentical_forc_lwrad(this)
    class(TestDownscaleForcings), intent(inout) :: this
    real(r8), allocatable :: eflx_sh_precip_conversion(:)
    real(r8), parameter :: topo = 17._r8
    real(r8), parameter :: forc_lwrad = 101._r8

    ! Setup
    call unittest_subgrid_setup_start()
    call unittest_add_gridcell()
    call create_landunit_ncols(ltype = 1, lweight = 1._r8, &
         ctypes = [0, 1], cweights = [0.7_r8, 0.3_r8])
    call unittest_subgrid_setup_end()
    call this%create_atm2lnd( &
         forc_topo = [topo], &
         forc_lwrad = [forc_lwrad])
    call this%create_topo(topo_col = [topo, topo])
    eflx_sh_precip_conversion = col_array()

    ! Exercise
    call downscale_forcings(bounds, this%topo_inst, this%atm2lnd_inst, &
         eflx_sh_precip_conversion)

    ! Verify
    associate(lwrad_col => this%atm2lnd_inst%forc_lwrad_downscaled_col)
    ! We expect a small difference due to normalization, so supply a tolerance
    @assertEqual([forc_lwrad, forc_lwrad], lwrad_col(bounds%begc:bounds%endc), tolerance=tol)
    end associate
  end subroutine twoTopos_at_atmTopo_give_nearlyIdentical_forc_lwrad

  ! ------------------------------------------------------------------------
  ! Tests with topo at atmosphere topo: downscaled forcing should be identical to
  ! atmospheric forcing
  ! ------------------------------------------------------------------------

  @Test
  subroutine topo_at_atmTopo_gives_identical_forc_t(this)
    class(TestDownscaleForcings), intent(inout) :: this
    real(r8), allocatable :: eflx_sh_precip_conversion(:)
    real(r8), parameter :: topo = 17._r8
    real(r8), parameter :: forc_t = 301._r8

    ! Setup
    call setup_single_veg_patch(pft_type=1)
    call this%create_atm2lnd( &
         forc_topo = [topo], &
         forc_t = [forc_t])
    call this%create_topo(topo_col = [topo])
    eflx_sh_precip_conversion = col_array()

    ! Exercise
    call downscale_forcings(bounds, this%topo_inst, this%atm2lnd_inst, &
         eflx_sh_precip_conversion)

    ! Verify
    @assertEqual(forc_t, this%atm2lnd_inst%forc_t_downscaled_col(begc))

  end subroutine topo_at_atmTopo_gives_identical_forc_t

  @Test
  subroutine topo_at_atmTopo_gives_identical_forc_th(this)
    class(TestDownscaleForcings), intent(inout) :: this
    real(r8), allocatable :: eflx_sh_precip_conversion(:)
    real(r8), parameter :: topo = 17._r8
    real(r8), parameter :: forc_th = 302._r8

    ! Setup
    call setup_single_veg_patch(pft_type=1)
    call this%create_atm2lnd( &
         forc_topo = [topo], &
         forc_th = [forc_th])
    call this%create_topo(topo_col = [topo])
    eflx_sh_precip_conversion = col_array()

    ! Exercise
    call downscale_forcings(bounds, this%topo_inst, this%atm2lnd_inst, &
         eflx_sh_precip_conversion)

    ! Verify
    @assertEqual(forc_th, this%atm2lnd_inst%forc_th_downscaled_col(begc))

  end subroutine topo_at_atmTopo_gives_identical_forc_th

  @Test
  subroutine topo_at_atmTopo_gives_identical_forc_q(this)
    class(TestDownscaleForcings), intent(inout) :: this
    real(r8), allocatable :: eflx_sh_precip_conversion(:)
    real(r8), parameter :: topo = 17._r8
    real(r8), parameter :: forc_q = 0.01_r8

    ! Setup
    call setup_single_veg_patch(pft_type=1)
    call this%create_atm2lnd( &
         forc_topo = [topo], &
         forc_q = [forc_q])
    call this%create_topo(topo_col = [topo])
    eflx_sh_precip_conversion = col_array()

    ! Exercise
    call downscale_forcings(bounds, this%topo_inst, this%atm2lnd_inst, &
         eflx_sh_precip_conversion)

    ! Verify
    @assertEqual(forc_q, this%atm2lnd_inst%forc_q_downscaled_col(begc))

  end subroutine topo_at_atmTopo_gives_identical_forc_q

  @Test
  subroutine topo_at_atmTopo_gives_identical_forc_pbot(this)
    class(TestDownscaleForcings), intent(inout) :: this
    real(r8), allocatable :: eflx_sh_precip_conversion(:)
    real(r8), parameter :: topo = 17._r8
    real(r8), parameter :: forc_pbot = 100000._r8

    ! Setup
    call setup_single_veg_patch(pft_type=1)
    call this%create_atm2lnd( &
         forc_topo = [topo], &
         forc_pbot = [forc_pbot])
    call this%create_topo(topo_col = [topo])
    eflx_sh_precip_conversion = col_array()

    ! Exercise
    call downscale_forcings(bounds, this%topo_inst, this%atm2lnd_inst, &
         eflx_sh_precip_conversion)

    ! Verify
    @assertEqual(forc_pbot, this%atm2lnd_inst%forc_pbot_downscaled_col(begc))

  end subroutine topo_at_atmTopo_gives_identical_forc_pbot

  @Test
  subroutine topo_at_atmTopo_gives_identical_forc_rho(this)
    class(TestDownscaleForcings), intent(inout) :: this
    real(r8), allocatable :: eflx_sh_precip_conversion(:)
    real(r8), parameter :: topo = 17._r8
    real(r8), parameter :: forc_rho = 1.1_r8

    ! Setup
    call setup_single_veg_patch(pft_type=1)
    call this%create_atm2lnd( &
         forc_topo = [topo], &
         forc_rho = [forc_rho])
    call this%create_topo(topo_col = [topo])
    eflx_sh_precip_conversion = col_array()

    ! Exercise
    call downscale_forcings(bounds, this%topo_inst, this%atm2lnd_inst, &
         eflx_sh_precip_conversion)

    ! Verify
    @assertEqual(forc_rho, this%atm2lnd_inst%forc_rho_downscaled_col(begc))

  end subroutine topo_at_atmTopo_gives_identical_forc_rho

  @Test
  subroutine topo_at_atmTopo_gives_identical_forc_lwrad(this)
    class(TestDownscaleForcings), intent(inout) :: this
    real(r8), allocatable :: eflx_sh_precip_conversion(:)
    real(r8), parameter :: topo = 17._r8
    real(r8), parameter :: forc_lwrad = 101._r8

    ! Setup
    call setup_single_veg_patch(pft_type=1)
    call this%create_atm2lnd( &
         forc_topo = [topo], &
         forc_lwrad = [forc_lwrad])
    call this%create_topo(topo_col = [topo])
    eflx_sh_precip_conversion = col_array()

    ! Exercise
    call downscale_forcings(bounds, this%topo_inst, this%atm2lnd_inst, &
         eflx_sh_precip_conversion)

    ! Verify
    @assertEqual(forc_lwrad, this%atm2lnd_inst%forc_lwrad_downscaled_col(begc))

  end subroutine topo_at_atmTopo_gives_identical_forc_lwrad

end module test_downscale_forcings
