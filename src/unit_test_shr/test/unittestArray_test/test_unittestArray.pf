module test_unittestArray

  ! Tests of unittestArrayMod

  use funit
  use unittestArrayMod
  use unittestSubgridMod
  use unittestSimpleSubgridSetupsMod
  use shr_kind_mod , only : r8 => shr_kind_r8

  implicit none

  @TestCase
  type, extends(TestCase) :: TestUnittestArray
   contains
     procedure :: setUp
     procedure :: tearDown
  end type TestUnittestArray

contains

  subroutine setUp(this)
    class(TestUnittestArray), intent(inout) :: this
  end subroutine setUp

  subroutine tearDown(this)
    class(TestUnittestArray), intent(inout) :: this

    call unittest_subgrid_teardown()
  end subroutine tearDown

  @Test
  subroutine col_array_returnsColArray(this)
    class(TestUnittestArray), intent(inout) :: this
    real(r8), allocatable :: arr(:)

    call unittest_subgrid_setup_start()
    call create_gridcell_single_veg_patch(1)
    call create_gridcell_single_veg_patch(2)
    call unittest_subgrid_setup_end()

    arr = col_array()

    @assertEqual((bounds%endc-bounds%begc+1), size(arr))

  end subroutine col_array_returnsColArray

  @Test
  subroutine col_array_setsVal(this)
    class(TestUnittestArray), intent(inout) :: this
    real(r8), allocatable :: arr(:)

    call unittest_subgrid_setup_start()
    call create_gridcell_single_veg_patch(1)
    call create_gridcell_single_veg_patch(2)
    call unittest_subgrid_setup_end()

    arr = col_array(17._r8)

    @assertEqual([17._r8, 17._r8], arr)
  end subroutine col_array_setsVal

  @Test
  subroutine col_array_uninit(this)
    class(TestUnittestArray), intent(inout) :: this
    real(r8), allocatable :: arr(:)

    call unittest_subgrid_setup_start()
    call create_gridcell_single_veg_patch(1)
    call create_gridcell_single_veg_patch(2)
    call unittest_subgrid_setup_end()

    arr = col_array()

    @assertIsNan(arr(1))
    @assertIsNan(arr(2))
  end subroutine col_array_uninit

  @Test
  subroutine test_logical_array_to_int(this)
    class(TestUnittestArray), intent(inout) :: this

    @assertEqual([1, 0, 1], logical_array_to_int([.true., .false., .true.]))
  end subroutine test_logical_array_to_int

end module test_unittestArray
